%%[
    /* {"title":"PCB Bulleted List","description":"create a table-based bulleted list"} */
    SET @Item_1_Text = "Item 1" /* {"description":"list item text.  put links in square brackets []"} */
    SET @Item_2_Text = "This has [a link] in it." /* {"description":"list item text.  put links in square brackets []"} */
    SET @Item_3_Text = "Item 3" /* {"description":"list item text.  put links in square brackets []"} */
    SET @Item_4_Text = "Item 4" /* {"description":"list item text.  put links in square brackets []"} */
    SET @Item_5_Text = "Item 5" /* {"description":"list item text.  put links in square brackets []"} */
    SET @Link_1_URL = "http://www.salesforce.com" /* {"description":"first link URL"} */
    SET @Link_1_Alias = "Link 1 Alias" /* {"description":"first link alias"} */
    SET @Link_2_URL = "" /* {"description":"second link URL"} */
    SET @Link_2_Alias = "" /* {"description":"second link alias"} */
    SET @Link_3_URL = "" /* {"description":"third link URL"} */
    SET @Link_3_Alias = "" /* {"description":"third link alias"} */
    SET @Padding = "20px 100px" /* {"description":"padding around the content block"} */
    SET @Bullet_Spacing = "20" /* {"description":"the spacing between bullets and items"} */
    SET @Alignment = "left" /* {"type":"selection","choices":["left","center","right"],"description":"the horizontal alignment of the list if none of the list items wrap"} */
    SET @Spacing = "10"  /* {"type":"slider","min":1,"max":100,"description":"the spacing between list items"} */
    SET @Line_Height = "20" /* {"type":"slider","min":1,"max":100,"description":"the line height of each list item.  this affects both the spacing between lines of a list item when it wraps and also affects the spacing between list items."} */
    SET @Bullet_Size = "30" /* {"type":"slider","min":1,"max":100,"description":"the size of the bullets"} */
    SET @Text_Size = "16" /* {"type":"slider","min":1,"max":100,"description":"the size of the text for list items"} */
    SET @Font_Family = "Arial, Helvetica, sans-serif" /* {"description":"the font family used for the text"} */
    SET @Bullet_Color = "#000001" /* {"type":"color","description":"the color of the bullets"} */
    SET @Text_Color = "#000001" /* {"type":"color","description":"the color of the text for list items"} */
    SET @Link_Color = "#0000ff" /* {"type":"color","description":"the color of links in list items"} */
    SET @Link_Decoration = "underline" /* {"type":"selection","choices":["underline","none"],"description":"the decoration type of links in list items"} */
    SET @Mobile_Class = "center-table pad-20L pad-20R" /* {"description":"the class(es) applied to the bulleted list on mobile displays"} */
    SET @Mobile_Bullet_Class = "" /* {"description":"the class(es) applied to the bullets on mobile displays"} */
    SET @Mobile_Item_Class = "" /* {"description":"the class(es) applied to the list items on mobile displays"} */
]%%

%%[
    SET @Bullets = Concat(@Item_1_Text, "|", @Item_2_Text, "|", @Item_3_Text, "|", @Item_4_Text, "|", @Item_5_Text)
    SET @Bullets_URL = Concat(@Link_1_URL, "|", @Link_2_URL, "|", @Link_3_URL)
    SET @Bullets_Alias = Concat(@Link_1_Alias, "|", @Link_2_Alias, "|", @Link_3_Alias)

    SET @Bullets_Rowset = BuildRowsetFromString(@Bullets, "|")
    SET @Bullets_URL_Rowset = BuildRowsetFromString(@Bullets_URL, "|")
    SET @Bullets_Alias_Rowset = BuildRowsetFromString(@Bullets_Alias, "|")
    SET @numRows = RowCount(@Bullets_Rowset)
    IF @numRows > 0 THEN
]%%
<table width="100%" cellpadding="0" cellspacing="0" border="0">
  <tr>
    <td class="%%=v(@Mobile_Class)=%%" align="%%=v(@Alignment)=%%" valign="top" style="padding: %%=v(@Padding)=%%;">
      <table cellpadding="0" cellspacing="0" border="0">
%%[
      SET @linkNum = 0
      FOR @i = 1 to @numRows DO
        SET @listItem = Field(Row(@Bullets_Rowset, @i), 1)
        IF NOT Empty(@listItem) THEN
          SET @padTop = IIF(@i == 0, "0", Concat(@Spacing, "px"))
]%%
        <tr>
          <td class="%%=v(@Mobile_Bullet_Class)=%%" align="left" width="%%=v(@Bullet_Spacing)=%%" valign="top" style="padding-top: %%=v(@padTop)=%%; font-family: %%=v(@Font_Family)=%%; font-weight: normal; font-size: %%=v(@Bullet_Size)=%%px; line-height: %%=v(@Line_Height)=%%px; color: %%=v(@Bullet_Color)=%%; mso-line-height-rule: exactly;">&bull;</td>
          <td class="%%=v(@Mobile_Item_Class)=%%" align="left" valign="top" style="padding-top: %%=v(@padTop)=%%; font-family: %%=v(@Font_Family)=%%; font-weight: normal; font-size: %%=v(@Text_Size)=%%px; line-height: %%=v(@Line_Height)=%%px; color: %%=v(@Text_Color)=%%; mso-line-height-rule: exactly;">
%%[
          SET @linkRows = BuildRowsetFromString(@listItem, "[")
          SET @rowCount = RowCount(@linkRows)

          SET @BeforeText = Field(Row(@linkRows, 1), 1)
]%%
            %%=v(@BeforeText)=%%
%%[
          FOR @j = 2 TO @rowCount DO
            SET @linkNum = Add(@linkNum, 1)
            SET @Link_URL = Field(Row(@Bullets_URL_Rowset, @linkNum), 1)
            SET @Link_Alias = Field(Row(@Bullets_Alias_Rowset, @linkNum), 1)

            SET @text = Field(Row(@linkRows, @j), 1)
            SET @close = IndexOf(@text, "]")

            SET @LinkText = Substring(@text, 1, Subtract(@close, 1))
            SET @AfterText = Substring(@text, Add(@close, 1), Length(@text))
]%%
            <a href="%%=RedirectTo(@Link_URL)=%%" alias="%%=v(@Link_Alias)=%%" target="_blank" style="color: %%=v(@Link_Color)=%%; text-decoration: %%=v(@Link_Decoration)=%%;">%%=v(@LinkText)=%%</a>%%=v(@AfterText)=%%
%%[       NEXT @j ]%%
          </td>
        </tr>
%%[     ENDIF ]%%
%%[   NEXT @i ]%%
%%[ ENDIF ]%%
      </table>
    </td>
  </tr>
</table>
